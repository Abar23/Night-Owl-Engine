#include "GaussianKernel.glsl"

layout (local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

layout(rgba32f, binding = 0) uniform readonly image2D sourceImage;

layout(rgba32f, binding = 1) uniform writeonly image2D destinationImage;

shared vec4 v[128 + 101];

void main() {
    ivec2 size = imageSize(sourceImage);
    ivec2 gpos = ivec2(gl_GlobalInvocationID.xy);
    uint lpos = gl_LocalInvocationID.x;
    int halfWidth = 100 / 2;
    
    // Load pixels into shared memory
    ivec2 pixelToRead = gpos + ivec2(-halfWidth, 0);
    if (pixelToRead.x < 0)
    {
        pixelToRead.x = 0;
    } 
    else if (pixelToRead.x >= size.x)
    {
        pixelToRead.x = size.x - 1;
    }

    v[lpos] = imageLoad(sourceImage, pixelToRead);
    if (lpos < 2 * halfWidth) {
        ivec2 otherPixelToRead = gpos + ivec2(128 - halfWidth, 0);
        if (otherPixelToRead.x < 0)
        {
            otherPixelToRead.x = 0;
        } 
        else if (otherPixelToRead.x >= size.x)
        {
            otherPixelToRead.x = size.x - 1;
        }

        v[lpos + 128] = imageLoad(sourceImage, otherPixelToRead);
    }

    // Ensure all threads have loaded their pixels before proceeding
    memoryBarrierShared();
    barrier();

    vec4 sum = vec4(0.0);

    // Compute convolution
    for (int i = -halfWidth; i <= halfWidth; ++i) 
    {
        sum += gaussianKernel[i + halfWidth] * v[lpos + i + halfWidth];
    }

    // Write to destination image
    if (gpos.x >= 0 && gpos.x < size.x)
    {
        imageStore(destinationImage, gpos, sum);
    }
}
